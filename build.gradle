import org.ajoberstar.grgit.Grgit

apply from: 'config/keystore.gradle'

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.0'
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.1'
        classpath 'org.jacoco:org.jacoco.core:0.8.10'
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    tasks.configureEach { task ->
        if (task.name.contains("androidJavadoc")) {
            task.enabled = false
        }
    }
}

ext.verifyKeystore()

try {
    def grgit = Grgit.open(currentDir: project.rootDir)
    def lastCommit = grgit.head()

    project.ext.versionNum = grgit.log(includes:['HEAD']).size()
    project.ext.versionName = grgit.describe()
    project.ext.versionDate = lastCommit.getDate()
    if (project.ext.versionName == null) {
        project.ext.versionName = 'DEV'
    }
} catch (Exception ignored) {
    project.ext.versionNum = 1
    project.ext.versionName = 'DEV'
    project.ext.versionDate = new Date()
}

project.ext {
    minSdkVersion = 21
    compileSdkVersion = 34

    googleVersions = [
        glide                 : '4.16.0',
        material              : '1.3.0',
    ]

    androidXVersions = [
        annotation            : '1.2.0',
        appCompat             : '1.3.0',
        browser               : '1.3.0',
    ]

    okioVersion = '2.10.0'
    jodaVersion = '2.10.10'
}

tasks.register('showVersion') {
    doLast {
        logger.lifecycle("Version ID: " + project.versionNum)
        logger.lifecycle("Version Name: " + project.versionName)
        logger.lifecycle("Version Date: " + project.versionDate)
    }
}
